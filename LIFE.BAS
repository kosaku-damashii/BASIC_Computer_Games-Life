1 WIDTH 80 :REM
rem 2 USECLASS STRDIM :REM
2 USECLASS STRD2: CALL STRD2::MAXLEN(100) :REM
3 GOSUB TAB,34:PRINT "LIFE" :REM
4 GOSUB TAB,15:PRINT "CREATIVE COMPUTING MORRISTOWN, NEW JERSEY" :REM
5 USEVAR X1,Y1,X2,Y2,X3,Y3,X4,Y4,I9 :REM
6 PRINT: PRINT: PRINT
8 PRINT "ENTER YOUR PATTERN:"
9 X1=1: Y1=1: X2=24: Y2=70
rem 10 DIM A(24,70):B=NEW(STRDIM, 24)
10 DIM A(24,70):B=NEW(STRD2, 24)
20 C=1:G=0:I9=0 :REM
30 Q$=INPUT$():B.LETSTR(C,Q$) :REM
40 IF STRNCMP(B.GETSTR$(C),"DONE",4)=0 THEN B.LETSTR(C, ""): GOTO 80 :REM
50 IF STRNCMP(GOSUB$(LEFT,B.GETSTR$(C),1),".",1)!=0 THEN GOTO 60 :REM
52 Q$=GOSUB$(RIGHT,B.GETSTR$(C), LEN(B.GETSTR$(C))-1): Q$=" "+Q$: B.LETSTR(C,Q$) :REM
60 C=C+1
70 GOTO 30
80 C=C-1: L=0
90 FOR X=1 TO C-1
100 IF LEN(B.GETSTR$(X))>L THEN L=LEN(B.GETSTR$(X)) :REM
110 NEXT :REM
120 X1=11-C/2
130 Y1=33-L/2
140 FOR X=1 TO C
150 FOR Y=1 TO LEN(B.GETSTR$(X))
160 IF STRNCMP(GOSUB$(MID,B.GETSTR$(X),Y,1)," ",1) != 0 THEN A(X1+X,Y1+Y)=1:P=P+1 :REM
170 NEXT :REM
180 NEXT :REM
200 PRINT:PRINT:PRINT
210 WAIT 60:CLS:PRINT "GENERATION:";G,"POPULATION:";P;: IF I9 THEN PRINT "INVALID"; :REM
215 X3=24: Y3=70: X4=1: Y4=1: P=0
220 G=G+1
225 FOR X=1 TO X1-1: PRINT: NEXT :REM
230 FOR X=X1 TO X2
240 PRINT
250 FOR Y=Y1 TO Y2
253 IF A(X,Y)=2 THEN A(X,Y)=0: GOTO 270
256 IF A(X,Y)=3 THEN A(X,Y)=1: GOTO 261
260 IF A(X,Y)!=1 THEN 270
261 GOSUB TAB,Y:PRINT "*"; :REM
262 IF X<X3 THEN X3=X
264 IF X>X4 THEN X4=X
266 IF Y<Y3 THEN Y3=Y
268 IF Y>Y4 THEN Y4=Y
270 NEXT :REM
290 NEXT :REM
295 FOR X=X2+1 TO 24: PRINT: NEXT :REM
299 X1=X3: X2=X4: Y1=Y3: Y2=Y4
301 IF X1<3 THEN X1=3: I9=-1
303 IF X2>22 THEN X2=22: I9=-1
305 IF Y1<3 THEN Y1=3: I9=-1
307 IF Y2>68 THEN Y2=68: I9=-1
309 P=0
500 FOR X=X1-1 TO X2+1
510 FOR Y=Y1-1 TO Y2+1
520 C=0
530 FOR I=X-1 TO X+1
540 FOR J=Y-1 TO Y+1
550 IF A(I,J)=1 OR A(I,J)=2 THEN C=C+1
560 NEXT :REM
570 NEXT :REM
580 IF A(X,Y)=0 THEN 610
590 IF C<3 OR C>4 THEN A(X,Y)=2: GOTO 600
595 P=P+1
600 GOTO 620
610 IF C=3 THEN A(X,Y)=3: P=P+1
620 NEXT :REM
630 NEXT :REM
635 X1=X1-1:Y1=Y1-1:X2=X2+1:Y2=Y2+1
640 GOTO 210
650 END

REM Subroutines For MachiKania

LABEL LEFT
VAR X,Y,R
X$=ARGS$(1)
Y=ARGS(2)
R$=X$(0,Y)
RETURN R$

LABEL MID
VAR X,Y,Z
X$=ARGS$(1)
Y=ARGS(2)-1
Z=ARGS(3)
R$=X$(Y,Z)
RETURN R$

LABEL RIGHT
VAR X,Y,R
X$=ARGS$(1)
Y=ARGS(2)
R$=X$(-Y)
RETURN R$

LABEL TAB
VAR Z
Z=ARGS(1)
CURSOR Z,SYSTEM(27)
RETURN
